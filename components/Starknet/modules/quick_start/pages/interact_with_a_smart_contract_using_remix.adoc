= Interacting with a smart contract with Remix

To compile, deploy, and interact with our smart contract, utilize the Starknet Remix Plugin. This tool is essential for starting Starknet development without the need for any local installations on your machine.

== Introduction

. Go to the https://remix.ethereum.org/#activate=Starknet&lang=en&optimize=false&runs=200&evmVersion=null&version=soljson-v0.8.24+commit.e11b9ed9.js[Remix IDE] website with the Starknet plugin activated.
. Navigate to the `File Explorer` tab to review the details of the example project.
. Select the `Settings` option and choose the Cairo version, which is the latest version available in Remix.
. In the file `Scarb.toml`, you can find the version of this example project specified.
. It is crucial to make sure that you match the version originally specified with the one found in our `Scarb.toml` file. Any inconsistencies should be corrected by configuration.

== Customizing for Ownable Contract

[NOTE]
====
A default example project is provided for the purposes of this tutorial. To achieve this, certain files and directories will need to be modified or removed.
====

. Rename the root directory as `ownable`. Within your `Scarb.toml`, under the [package] section, designate the name as `ownable`.
. Delete the `balance.cairo` and `forty_two.cairo` files, if they exist.
. Open `lib.cairo` and remove all its contents, making it empty.

== Introduction to Starknet Smart Contracts

Explore the foundational Ownable contract pattern crafted in Cairo for Starknet. It includes:

* An ownership system.
* A method to transfer ownership.
* A method to check the current owner.
* An event notification for ownership changes.

=== Cairo Example Contract

[source,cairo]
----
use starknet::ContractAddress;

#[starknet::interface]
trait OwnableTrait<T> {
    fn transfer_ownership(ref self: T, new_owner: ContractAddress);
    fn get_owner(self: @T) -> ContractAddress;
}

#[starknet::contract]
mod Ownable {
    use super::ContractAddress;
    use starknet::get_caller_address;

    #[event]
    #[derive(Drop, starknet::Event)]
    enum Event {
      OwnershipTransferred: OwnershipTransferred,
    }

    #[derive(Drop, starknet::Event)]
    struct OwnershipTransferred {
        #[key]
        prev_owner: ContractAddress,
        #[key]
        new_owner: ContractAddress,
    }

    #[storage]
    struct Storage {
        owner: ContractAddress,
    }

    #[constructor]
    fn constructor(ref self: ContractState, init_owner: ContractAddress) {
        self.owner.write(init_owner);
    }

    #[abi(embed_v0)]
    impl OwnableImpl of super::OwnableTrait<ContractState> {
        fn transfer_ownership(ref self: ContractState, new_owner: ContractAddress) {
            self.only_owner();
            let prev_owner = self.owner.read();
            self.owner.write(new_owner);
            self.emit(Event::OwnershipTransferred(OwnershipTransferred {
                prev_owner: prev_owner,
                new_owner: new_owner,
            }));
        }

        fn get_owner(self: @ContractState) -> ContractAddress {
            self.owner.read()
        }
    }

    #[generate_trait]
    impl PrivateMethods of PrivateMethodsTrait {
        fn only_owner(self: @ContractState) {
            let caller = get_caller_address();
            assert(caller == self.owner.read(), 'Caller is not the owner');
        }
    }
}
----

=== Components Breakdown

.Dependencies and Interface:
* `starknet::ContractAddress`: Represents a Starknet contract address.
* `OwnableTrait`: Defines essential functions for transferring and retrieving ownership.

.Events:
* `OwnershipTransferred`: Indicates changes in ownership, providing details about the previous and new owners.

.Storage:
* Stores the contract's state, including the address of the current owner.

.Constructor:
* Sets up the contract by assigning an initial owner.

.External Functions:
* Contains functions for transferring ownership and fetching information about the current owner.

.Private Methods:
* `only_owner`: Validates if the caller is the current owner.

== Compilation Process

To compile using Remix:

.File Creation

* Go to the "File Explorer" tab in Remix's interface.
* Find the file named lib.cairo and insert the provided code into it.

.Compilation

* Navigate to the `Starknet` tab within Remix, then click on `Home`.
* In the 1 Compile section, choose compile a single file.
* Grant the necessary permissions when prompted. Opt to Remember my choice for smoother processes in the future.
* After compilation, you'll see an "artifacts" directory containing the compiled contract in two formats: Sierra (JSON file) and CASM. For Starknet deployment, Remix will use the Sierra file.
* More detailed explanations on this process will follow in subsequent chapters. For now, Remix efficiently handles both compilation and deployment.

== Deployment on the Development Network

To initiate the operational phase of your smart contract, you need to establish an initial owner, which is achieved through the Constructor function.

Here's a systematic approach to deploying your smart contract on the development network

.Select the Network

* Click on the "Remote Devnet" button in the Starknet tab.

.Choose a Devnet Account

* In the "Devnet account selection" section, you'll see a list of accounts specific to the designated devnet.
* Select an account from the list and note down its address for later use.  

.Declare

* Click on "Declare lib.cairo".
* After declaration, Remix's terminal will provide various logs with important details such as:
* `transaction_hash`: This unique hash identifies the transaction and can be used to track its status.
* `class_hash`: Similar to an identifier, the class hash contains the definition of the smart contract.

[source,bash]
----
------------------------ Declaring contract: ownable_Ownable ------------------------
{
  "transaction_hash": "0x36dabf43f4962c97cf67ba132fb520091f268e7e33477d77d01747eeb0d7b43",
  "class_hash": "0x540779cd109ad20f46cb36d8de1ce30c75469862b4dc75f2f29d1b4d1454f60"
}
---------------------- End Declaring contract: ownable_Ownable ----------------------
----

.To initiate deployment

* Paste the copied address into the init_owner variable.
* Click on **"Deploy"**.

After deployment, Remix's terminal will display various logs containing important details:

* `transaction_hash`: This unique hash identifies the transaction and can be used to track its status.
* `contract_address`: The address of the deployed contract. You'll use this address to interact with your contract.
* `data`: Contains the `init_owner` address provided to the constructor.

[source,bash]
----
{
  "transaction_hash": "0x624f5b9f57e53f6b5b62e588f0f949442172b3ad5d04f0827928b4d12c2fa58",
  "contract_address": [
    "0x699952dc736661d0ed573cd2b0956c80a1602169e034fdaa3515bfbc36d6410"
  ]
    ...
  "data": [
        "0x6b0ee6f418e47408cf56c6f98261c1c5693276943be12db9597b933d363df",
         ...
      ]
    ...
}
----

To effectively deploy your smart contract on the development network, follow the procedure outlined above.

== Interaction with the Contract

Now that the contract is operational on the development network, you can engage with it. Here's a guide on how to interact with your contract on Starknet:

.Initiating Interaction

* Open the "Starknet" plugin tab in your interface.
* Choose the "Interact" option to begin interaction.

.Calling the `get_owner` Function

* Select the get_owner function. Since it doesn't require any arguments, the calldata field remains empty. (Since this function reads data, its invocation is called a "call").
* Execute the "Call" operation. The terminal will promptly display the outcome, revealing the owner's address provided during the contract's deployment within the calldata for the constructor:

[source,json]
----
{
  "resp": {
    "result": [
      "0x6b0ee6f418e47408cf56c6f98261c1c5693276943be12db9597b933d363df"
    ]
  },
  "contract": "lib.cairo",
  "function": "get_owner"
}
----

This call doesn't consume gas since the function doesn't modify the contract's state.

.Invoking the `transfer_ownership` Function

* Choose the "Write" option in the interaction area, where functions that alter the contract's state are listed.
* Select the transfer_ownership function, which requires providing the new owner address as input.
* Fill in the new_owner field with the relevant address. (Choose any address from the "Devnet account selection" listed in the Environment tab for this purpose.)
* Click the "Call" button. The terminal will then display the transaction hash indicating the change in the contract's state. Since this interaction modifies the contract's state, it's called an "invoke" and requires the signature of the account executing the function.
* For such transactions, the terminal logs will include a "status" parameter indicating the outcome. A status of "ACCEPTED_ON_L2" signifies approval by the Sequencer, the entity responsible for receiving and processing transactions, indicating inclusion in an upcoming block. Conversely, a "REJECTED" status indicates disapproval by the Sequencer, preventing inclusion in the next block. Typically, transactions of this nature are approved, resulting in a modification of the contract's state.

[source,json]
----
{
  "resp": {
    "result": [
      "0x5495d56633745aa3b97bdb89c255d522e98fd2cb481974efe898560839aa472"
    ]
  },
  "contract": "lib.cairo",
  "function": "get_owner"
}
----

== Deployment on Starknet Testnet

After testing your smart contract on a development network, the next step is deploying it onto the Starknet Testnet. The Starknet Testnet is a public platform accessible to all, providing an excellent environment for testing smart contracts and encouraging collaboration among developers.

Before proceeding, you must create a Starknet account.

=== Smart Wallet Configuration

Before deploying your smart contract on Starknet, it's crucial to address transaction costs. While deploying on the Starknet Sepolia Testnet is free, having an operational smart wallet account is essential. You can set up a smart wallet using either of the following platforms:

* https://www.argent.xyz/argent-x/[Argent]
* https://braavos.app/[Braavos]

Both options offer robust Starknet wallets with advanced security measures and enhanced accessibility features enabled by the capabilities of the Cairo VM. (More insights on this subject will be provided in subsequent sections of this guide.)

Here's how to set up your smart wallet:

. Install the recommended Chrome/Brave extension corresponding to your chosen wallet.
. Follow the instructions provided by your wallet provider to deploy your account.
. Use the https://starknet-faucet.vercel.app/[Starknet Faucet] to fund your account.
. Execute the deployment of your account onto the network, typically completed within approximately 10 seconds.

Once the setup is complete, you are primed to deploy your smart contracts onto the Starknet Testnet.

=== Deployment and Interaction

. Proceed as per the aforementioned deployment steps.
. Within the `Environment selection` tab, opt for `Wallet`.
. Select your Starknet account and proceed with the deployment and interaction processes for your contract.

Monitoring transaction hashes and addresses can be facilitated through various Starknet block explorers such as:

* https://testnet.starkscan.co/[Starkscan]
* https://sepolia.voyager.online/[Voyager]
* https://viewblock.io/starknet[ViewBlock]
* https://www.oklink.com/starknet[Oklink]

These utilities offer a graphical depiction of transactions and modifications to the contract state. Noteworthy is the visibility provided when altering contract ownership through the `transfer_ownership` function, as the emitted event by the contract becomes observable within the block explorer. This mechanism serves as a potent means to monitor contractual events.
